if(TARGET zlibstatic OR TARGET zlib)
    if(NOT TARGET ZLIB::ZLIB)
        if(TARGET zlibstatic)
            add_library(ZLIB::ZLIB ALIAS zlibstatic)
        else()
            add_library(ZLIB::ZLIB ALIAS zlib)
        endif()
    endif()
    return()
endif()

include(FetchContent)

set(ZLIB_VERSION 1.3.1 CACHE STRING "zlib version")
set(ZLIB_BUILD_EXAMPLES OFF CACHE BOOL "" FORCE)
set(ZLIB_BUILD_TESTS OFF CACHE BOOL "" FORCE)
FetchContent_Declare(zlib URL https://zlib.net/zlib-${ZLIB_VERSION}.tar.gz)
FetchContent_MakeAvailable(zlib)
if(TARGET zlibstatic AND NOT TARGET ZLIB::ZLIB)
    add_library(ZLIB::ZLIB ALIAS zlibstatic)
elseif(TARGET zlib AND NOT TARGET ZLIB::ZLIB)
    add_library(ZLIB::ZLIB ALIAS zlib)
endif()
# Provide legacy FindZLIB variables so subprojects (libspng) succeed without system zlib
if(MSVC)
    # Multi-config: use generator expressions for path and debug suffix
    set(ZLIB_LIBRARY "${zlib_BINARY_DIR}/$<CONFIG>/zlibstatic$<$<CONFIG:Debug>:d>.lib" CACHE STRING "" FORCE)
else()
    set(ZLIB_LIBRARY "${zlib_BINARY_DIR}/libz.a" CACHE STRING "" FORCE)
endif()
set(ZLIB_LIBRARIES "${ZLIB_LIBRARY}" CACHE STRING "" FORCE)
set(ZLIB_INCLUDE_DIR "${zlib_SOURCE_DIR};${zlib_BINARY_DIR}" CACHE STRING "" FORCE)
set(ZLIB_FOUND TRUE CACHE BOOL "" FORCE)
